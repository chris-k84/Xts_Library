<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.17">
  <POU Name="Machine" Id="{e8d4edde-6e68-4e80-9f5b-2e9346044142}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Machine
VAR
	Movers : ARRAY[0..NumberOFSystemMovers] OF XtsMover;
	iMovers : ARRAY[0..NumberOFSystemMovers] OF I_XtsMover := [Movers[0],Movers[1],
											Movers[2],Movers[3],
											Movers[4],Movers[5],
											Movers[6],Movers[7]];
	Station1 : StationSample(Position := 850.0, 
									DummyOperationTime := T#1500MS);
	Station2 : StationSample(Position := 1200.0, 
									DummyOperationTime := T#1500MS);
	Station3 : Process(Position := 1500.0, 
						EndPosition := 2000.0, 
						ProcessVelocity := 80);
	Station4 : StationSample(Position := 2100.0, 
									DummyOperationTime := T#1500MS);
	Station5 : StationSample(Position := 2400.0, 
									DummyOperationTime := T#1500MS);
	systemXtsioEnvVar : XtsIoEnvVar;	
	systemXtsHardware : XTSHardware;				
	systemCAGroup : CollisionGroup;															
	MyXts : ThisXTS(systemXtsioEnvVar,systemXtsHardware,systemCAGroup) := (MyXts := 1, 
																			ExpectedNumberOfParts := 1, 
																			ExpectedNumberOfTracks := 1, 
																			HomePosition := SystemConstants.XtsHomePosition);
	MoverSequences : ARRAY[0..NumberOFSystemMovers] OF StationBehaviour := [8((MyStartPosition := SystemConstants.XtsHomePosition))];	
	initSequence : INT;
	InitComplete : BOOL;
	Trigger1 : TriggerPoint(Position := 100.0, Range := 200);
	Trigger2 : TriggerPoint(Position := 1500.0, Range := 200);
END_VAR
VAR CONSTANT
	NumberOFSystemMovers : INT := 7;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Cycle" Id="{800ade8f-bb7c-4681-a005-35c2c0ff3879}">
      <Declaration><![CDATA[METHOD Cycle : HRESULT
VAR
	i : INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[systemXtsHardware.Cycle();
systemCAGroup.Cycle();
MyXts.Cycle();
FOR i := 0 TO NumberOFSystemMovers DO
	Movers[i].Cycle();
	MoverSequences[i].Cycle();
END_FOR
Station1.Cycle();
Station2.Cycle();
Station3.Cycle();
Station4.Cycle();
Station5.Cycle();
Trigger1.Cycle();
Trigger2.Cycle();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialise" Id="{4069299b-b0bd-441e-b636-7c39202b14ad}">
      <Declaration><![CDATA[METHOD Initialise : BOOL
VAR
	i : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE initSequence OF
	0:
		FOR i := 0 TO NumberOFSystemMovers DO
			MoverSequences[i].MyMover := Movers[i]; 
		END_FOR
		initSequence := 1;
	1:
		IF SUCCEEDED(systemXtsioEnvVar.Initialise()) THEN
			initSequence := 2;
		END_IF
	2:
		IF SUCCEEDED(systemXtsHardware.Initialise()) AND SUCCEEDED(MyXts.Initialise()) THEN
			initSequence := 3;
		END_IF
	3:
		FOR i := 0 TO NumberOFSystemMovers DO
			MyXts.RegisterMovers(MyMover := iMovers[i]);
			Trigger1.RegisterMovers(MyMover := iMovers[i]);
			Trigger2.RegisterMovers(MyMover := iMovers[i]);
		END_FOR
		initSequence := 4;
	4:
		FOR i := 0 TO NumberOFSystemMovers DO
			MoverSequences[i].RegisterAStation(Station := Station1);
			MoverSequences[i].RegisterAStation(Station := Station2);
			MoverSequences[i].RegisterAStation(Station := Station3);
			MoverSequences[i].RegisterAStation(Station := Station4);
			MoverSequences[i].RegisterAStation(Station := Station5);
		END_FOR
		initSequence := 5;
	5:
		FOR i := 0 TO NumberOFSystemMovers DO
			Movers[i].Initialise();
		END_FOR
		initSequence := 6;
	6:
		InitComplete := TRUE;
		Initialise := TRUE;
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{9f1f9d34-e777-4487-9353-8daf70debb65}">
      <Declaration><![CDATA[METHOD Reset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT MyXts.IsXtsReady() THEN
	MyXts.EnableXTS();
ELSE
	MyXts.MoverHomeSequence();
	Reset := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{6289f1df-c8c2-4a24-9238-0ac10992d955}">
      <Declaration><![CDATA[METHOD Start : BOOL
VAR
	i : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 0 TO NumberOFSystemMovers DO
	MoverSequences[i].StartIndexing();
END_FOR
Start := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Stop" Id="{5e47e9ec-af05-4f55-a014-ec50066f7440}">
      <Declaration><![CDATA[METHOD Stop : BOOL
VAR
	i : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF MyXts.DisableXTS() THEN
	Stop := TRUE;
	FOR i := 0 TO NumberOFSystemMovers DO
		MoverSequences[i].Reset();
	END_FOR
END_IF
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>