<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="XtsMotorModule" Id="{447db564-ab24-433c-b078-4cedd1c7411e}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK XtsMotorModule EXTENDS CyclicBase IMPLEMENTS I_Initialisable
VAR
	_MyModuleIoInterface : I_TcIoXtsPartModule;
	_IsInitialised : BOOL;
	CurrentLive: LREAL;
	MaxCurrent: LREAL;
	Nominal24Vdc : REAL := 24.0;
	Nominal48Vdc : REAL := 48.0;
	InSim : BOOL;
	Module24Vdc: REAL;
	Module48Vdc: REAL;
	ModuleState: Tc3_XTS_Utility.DriveState;
	ModuleStatus: Tc3_XTS_Utility.CoeStatusInfo;
	ErrorTrigger : R_TRIG;
	ModuleError: Tc3_XTS_Utility.I_AT2xxxDiagHistory;
	ReadDiagHistory: BOOL;
	ModuleDiagMessages: ARRAY [1..25] OF Tc3_XTS_Utility.ST_DiagHistoryMsg;
	NullMessage : ST_DiagHistoryMsg;
	NewDiagMessageCount : UINT;
	ModuleName: STRING;
	DriveId: Tc3_XTS_Utility.ST_DriveOnlineIdentity;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="AckDiagMsgs" Id="{d8ef1bd0-5cfc-41d5-b153-4987f2146f4f}">
      <Declaration><![CDATA[METHOD AckDiagMsgs : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF ModuleError = 0 THEN
	RETURN;
END_IF

ModuleError.AckAllMsgs := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Cycle" Id="{1188f197-bbd0-4970-903a-af249f1ed9e0}">
      <Declaration><![CDATA[METHOD PUBLIC  Cycle : HRESULT
VAR_INPUT
END_VAR
VAR
	i : INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _MyModuleIoInterface = 0 THEN
	Cycle := -1;
	RETURN;
END_IF

IF InSim THEN
	Cycle := -2;
	RETURN;
END_IF

CurrentLive:= _MyModuleIoInterface.GetOverallCurrent();
MaxCurrent:=  _MyModuleIoInterface.GetMaxOverallCurrentLast500ms();
Module24Vdc := _MyModuleIoInterface.GetAuxiliaryVoltage24V();
Module48Vdc := _MyModuleIoInterface.GetDCLinkVoltage();
ModuleState := _MyModuleIoInterface.GetDriveState();
ModuleStatus := _MyModuleIoInterface.GetDriveStatusInfo();
IF ModuleError.UnackMsgsAvailable THEN
	NewDiagMessageCount := ModuleError.UnackMsgCount;
	FOR i := 1 TO 25 DO
		ModuleDiagMessages[i] := NullMessage;
	END_FOR
	ModuleDiagMessages := ModuleError.DiagHistory.aDiagHistoryMsg;
END_IF

ErrorTrigger(CLK := ModuleState = DriveState.Fault OR ModuleState = DriveState.FaultReactionActive);
IF ErrorTrigger.Q OR ReadDiagHistory THEN
	ReadDiagHistory := TRUE;
	if ModuleError.UpdateDiagHistory() THEN
		ReadDiagHistory := FALSE;
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetLatestDiagMessage" Id="{c5f72fde-7f4b-4c41-8777-bc94a2aa888f}">
      <Declaration><![CDATA[METHOD GetLatestDiagMessage : UDINT
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetLatestDiagMessage := ModuleDiagMessages[1].wDiagMessageCode;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialise" Id="{0d230f78-299f-458d-bcac-a537a1290197}">
      <Declaration><![CDATA[METHOD Initialise : HRESULT
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _MyModuleIoInterface.P_IsInitialized THEN
	ModuleError := _MyModuleIoInterface.CoE.DiagHistoryData;
	ReadDiagHistory := TRUE;
	ModuleName := _MyModuleIoInterface.P_ObjectName;
	DriveId := _MyModuleIoInterface.GetDriveOnlineIdentity();
	Initialise := 2;
	_IsInitialised := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Is24VOK" Id="{9b08696d-e0e1-46f9-950f-7b12a84e7180}">
      <Declaration><![CDATA[METHOD Is24VOK : HRESULT
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Is24VOK := 1;
IF Module24Vdc < (Nominal24Vdc * 0.8) THEN
	Is24VOK := -1;
END_IF
IF Module24Vdc > (Nominal24Vdc * 1.2) THEN
	Is24VOK := -1;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Is48VOK" Id="{f76f25e2-8796-412c-b16e-72d1b643b91d}">
      <Declaration><![CDATA[METHOD Is48VOK : HRESULT
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Is48VOK := 1;
IF Module48Vdc < (Nominal48Vdc * 0.9) THEN
	Is48VOK := -1;
END_IF
IF Module48Vdc > (Nominal48Vdc * 1.1) THEN
	Is48VOK := -1;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsInitialised" Id="{2ed8abff-cfcf-4abb-a4a9-d431bb45f223}">
      <Declaration><![CDATA[METHOD IsInitialised : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsInitialised := _IsInitialised;]]></ST>
      </Implementation>
    </Method>
    <Property Name="MyModuleInterface" Id="{2168d719-80a8-412e-a875-7c102bb8a0d2}">
      <Declaration><![CDATA[PROPERTY MyModuleInterface : I_TcIoXtsPartModule]]></Declaration>
      <Get Name="Get" Id="{89cf633f-414b-41a7-b6da-6e0666d662cd}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[MyModuleInterface := _MyModuleIoInterface;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{c9ca3467-a5fd-4003-89ce-a9baa660bd96}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_MyModuleIoInterface := MyModuleInterface;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="SetInSim" Id="{13eee03f-0b41-4595-95d5-55a5ef2b6f3f}">
      <Declaration><![CDATA[METHOD SetInSim : HRESULT
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InSim := true;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>