<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="TmsEmbarkStation" Id="{4d15a973-29cb-4ffe-934a-8f072d992ec0}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TmsEmbarkStation IMPLEMENTS I_Station, I_Cyclic, I_Resettable, I_Object
VAR
	_IsClear : BOOL := TRUE;
	_Complete : BOOL;
	_Operating : BOOL;
	_Ready : BOOL := TRUE;
	_Position : LREAL := 0.0;
	_HasMover : BOOL;
	MoverArrive : R_TRIG;
	_MyMover : I_XtsMover;
	SwitchAxisPosCtrl : I_Lift;
	_Track1SwitchPosition : MC_LREAL;
	_Track2SwitchPosition : MC_LREAL;
	ExecuteSequence : BOOL;
	_MyTrackNumber : INT := 1;
	State : EmbarkStationStates;
	_MoverTravelPosition : MC_LREAL;
	_BoardVelocity : MC_LREAL := 100.0;
	_SwitchVelocity : MC_LREAL := 200.0;
	_MoverLeavePosition : LREAL;
	Called : BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[{analysis -2}]]></ST>
    </Implementation>
    <Folder Name="Status" Id="{fc30821c-889b-457b-acd5-7e0794e2da97}" />
    <Property Name="BoardVelocity" Id="{af5577ef-90f6-4070-9e3d-dce62d068c94}">
      <Declaration><![CDATA[PROPERTY BoardVelocity : MC_LREAL]]></Declaration>
      <Get Name="Get" Id="{442f20d9-4d5c-462e-ace0-37721614361e}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[BoardVelocity := _BoardVelocity;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{4f476cb8-1c36-46ee-b33f-8098bec190be}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_BoardVelocity := BoardVelocity;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="Cycle" Id="{3f7d84b9-4220-4895-8d1f-d1319db89048}">
      <Declaration><![CDATA[METHOD Cycle : HRESULT]]></Declaration>
      <Implementation>
        <ST><![CDATA[MoverArrive(CLK := _HasMover);
IF MoverArrive.Q THEN
	_IsClear := FALSE;
	_Ready := FALSE;
	_Operating := TRUE;
	_Complete := FALSE;
END_IF
Operation();]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_Init" Id="{c56e5d7e-44de-4ea5-9429-5a143178668d}">
      <Declaration><![CDATA[(*FB_Init is always available implicitly and it is used primarily for initialization.
The return value is not evaluated. For a specific influence, you can also declare the
methods explicitly and provide additional code there with the standard initialization
code. You can evaluate the return value.*)
METHOD FB_Init : BOOL
VAR_INPUT
	(* TRUE: the retain variables are initialized (reset warm / reset cold)*)
	bInitRetains	: BOOL;
	(* TRUE: the instance will be copied to the copy code afterward (online change)   *)
	bInCopyCode	: BOOL;
	Position	: LREAL;
	SwitchAxis : I_Lift;
	MyTrackNumber : INT;
	Track1SwitchPosition : LREAL;
	Track2SwitchPsoition : LREAL;
	MoverOnSwitchPosition : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Position := Position;
SwitchAxisPosCtrl := SwitchAxis;
_MyTrackNumber := MyTrackNumber;
_Track1SwitchPosition := Track1SwitchPosition;
_Track2SwitchPosition := Track2SwitchPsoition;
_MoverTravelPosition := moverOnSwitchPosition;]]></ST>
      </Implementation>
    </Method>
    <Method Name="HasMover" Id="{01ba8fbd-2454-4e68-9b4a-9db0f953212c}" FolderPath="Status\">
      <Declaration><![CDATA[METHOD HasMover : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[HasMover := _HasMover;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsClear" Id="{43301cfc-5dfe-497a-ba25-2baea0d242aa}" FolderPath="Status\">
      <Declaration><![CDATA[METHOD IsClear : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsClear := _IsClear;]]></ST>
      </Implementation>
    </Method>
    <Method Name="isComplete" Id="{44421f60-5729-4d12-9b71-06a85fd9d019}" FolderPath="Status\">
      <Declaration><![CDATA[METHOD isComplete : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[isComplete := _Complete;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsOperating" Id="{df31fe6f-4d2e-47a8-9a3f-cb59b84fd3a0}" FolderPath="Status\">
      <Declaration><![CDATA[METHOD IsOperating : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsOperating := _Operating;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsReady" Id="{ecaac7cf-e1f3-4b8e-82e7-477bdefe5978}" FolderPath="Status\">
      <Declaration><![CDATA[METHOD IsReady : BOOL]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsReady := _Ready;]]></ST>
      </Implementation>
    </Method>
    <Method Name="MyMover" Id="{53ce83f8-f376-4c29-a057-798a57ce6dc6}">
      <Declaration><![CDATA[METHOD MyMover : HRESULT
VAR_INPUT
	mover	: I_XtsMover;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF mover = 0 THEN
	MyMover := -1;
	RETURN;
END_IF
IF _HasMover THEN
	MyMover := -2;
	RETURN;
END_IF
_HasMover := TRUE;
_MyMover := mover;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Operation" Id="{79e49ccb-98b2-4f0a-86f7-22dcc9a5dd9a}">
      <Declaration><![CDATA[METHOD Operation : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE State OF
	EmbarkStationStates.Init:
		_Operating := _IsClear := _Complete := _Ready := _HasMover := FALSE;
		State := EmbarkStationStates.Ready;
	
	EmbarkStationStates.Ready:	
		IF ExecuteSequence THEN
			_Operating := _Complete := _HasMover := FALSE;
			_IsClear := _Ready := TRUE;
			State := EmbarkStationStates.Waiting;
		END_IF
	
	EmbarkStationStates.Waiting:
		IF _HasMover THEN
			State := EmbarkStationStates.HasSwitch;
			_Operating := TRUE;
			_IsClear := FALSE;
			_Ready := FALSE;
			_Complete := FALSE;
		END_IF
	
	EmbarkStationStates.HasSwitch:
		IF (_MyTrackNumber = 1 AND SwitchAxisPosCtrl.isAtPosition(_Track1SwitchPosition))
			OR (_MyTrackNumber = 2 AND SwitchAxisPosCtrl.isAtPosition(_Track2SwitchPosition)) THEN
				SwitchAxisPosCtrl.Assigned := TRUE;
				State := EmbarkStationStates.Boarding;
				Called := FALSE;
		ELSIF NOT Called THEN
			State := EmbarkStationStates.Call;
		END_IF	
		
	EmbarkStationStates.Call:
		IF NOT SwitchAxisPosCtrl.Assigned AND_THEN SwitchAxisPosCtrl.IsStationary() THEN
			IF _MyTrackNumber = 1 THEN
				SwitchAxisPosCtrl.Call(Position := _Track1SwitchPosition, Velocity := _SwitchVelocity);
			ELSE
				SwitchAxisPosCtrl.Call(Position := _Track2SwitchPosition, Velocity := _SwitchVelocity);
			END_IF
			SwitchAxisPosCtrl.Assigned := TRUE;
			Called := TRUE;
			State := EmbarkStationStates.HasSwitch;
		END_IF
	
	EmbarkStationStates.Boarding:
		_MyMover.MoveTo(RequiredPosition := _MoverTravelPosition, MoveVelocity := _BoardVelocity, NonModuloMove := FALSE);
		IF _MoverTravelPosition > _MyMover.GetActiveTrackLength() THEN
			IF _MyMover.isMoverAtPosition(position := _MoverTravelPosition - _MyMover.GetActiveTrackLength()) THEN
				State := EmbarkStationStates.BoardingComplete;
			END_IF
		ELSE
			IF _MyMover.isMoverAtPosition(position := _MoverTravelPosition) THEN
				State := EmbarkStationStates.BoardingComplete;
			END_IF
		END_IF
		
	EmbarkStationStates.BoardingComplete:
		IF _MyTrackNumber = 1 THEN
			SwitchAxisPosCtrl.MoveToPosition(Position := _Track2SwitchPosition, Velocity := _SwitchVelocity);
		ELSE
			SwitchAxisPosCtrl.MoveToPosition(Position := _Track1SwitchPosition, Velocity := _SwitchVelocity);
		END_IF
		State := EmbarkStationStates.Moving;
		
	EmbarkStationStates.Moving:
		IF (_MyTrackNumber = 1 AND SwitchAxisPosCtrl.isAtPosition(position := _Track2SwitchPosition)) OR
			(_MyTrackNumber = 2 AND SwitchAxisPosCtrl.isAtPosition(position := _Track1SwitchPosition)) THEN
			State := EmbarkStationStates.ActivateTrackForMover;
		END_IF
		
	EmbarkStationStates.ActivateTrackForMover:
		IF _MyTrackNumber = 1 THEN
			IF SUCCEEDED(_MyMover.ActivateTrack(Track := 2)) THEN
				State := EmbarkStationStates.CheckActiveTrack;
			END_IF
		ELSE
			IF SUCCEEDED(_MyMover.ActivateTrack(Track := 1)) THEN
				State := EmbarkStationStates.CheckActiveTrack;
			END_IF
		END_IF
		
	EmbarkStationStates.CheckActiveTrack:
		IF _MyTrackNumber = 1 AND_THEN _MyMover.MoversActiveTrack = 2 THEN
			State := EmbarkStationStates.CheckMoverLeft;
			_MoverLeavePosition := _MyMover.AxisRef.NcToPlc.ActPos;
		ELSIF  _MyMover.MoversActiveTrack = 1 THEN
			State := EmbarkStationStates.CheckMoverLeft;
			_MoverLeavePosition := _MyMover.AxisRef.NcToPlc.ActPos;
		END_IF
	
	EmbarkStationStates.CheckMoverLeft:
		_Complete := TRUE;
		IF _MyMover.AxisRef.NcToPlc.ActPos > _MoverLeavePosition + 100 THEN
			State := EmbarkStationStates.Done;
			_HasMover := FALSE;
			SwitchAxisPosCtrl.Assigned := FALSE;
		END_IF
			
	EmbarkStationStates.Done:
		IF _Complete AND NOT _HasMover THEN
			_Operating := FALSE;
			_IsClear := TRUE;
			_Ready := TRUE;
			State := EmbarkStationStates.Waiting;
		END_IF	
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Property Name="Position" Id="{57781d24-723f-4dbc-b59b-493697f347b8}">
      <Declaration><![CDATA[{warning 'Add property implementation'}
PROPERTY Position : MC_LREAL
]]></Declaration>
      <Get Name="Get" Id="{846aa0af-3762-492c-8088-91ff454623be}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Position := _Position;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="RegisterWithParent" Id="{ae1250b4-e8d3-4bd2-8110-48d016d30ba8}">
      <Declaration><![CDATA[{warning 'Add method implementation '}
METHOD RegisterWithParent
VAR_INPUT
	Runner	: Base_Library.I_HostObjects;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{95aecf88-84d1-40dd-83f3-696248ea5185}">
      <Declaration><![CDATA[METHOD Reset : HRESULT]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Complete := FALSE;
_IsClear := TRUE;
_Operating := FALSE;
_Ready := TRUE;
_HasMover := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="StartSwitch" Id="{c29e9395-8979-4e9a-90a8-1d97909d0ac9}">
      <Declaration><![CDATA[METHOD StartSwitch : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ExecuteSequence := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="StopSwitch" Id="{3cacf78b-01e4-4c36-b301-1826803c65db}">
      <Declaration><![CDATA[METHOD StopSwitch : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ExecuteSequence := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Property Name="SwitchVelocity" Id="{2b2ab201-99a2-4b79-87da-05eb4b2bdaf0}">
      <Declaration><![CDATA[PROPERTY SwitchVelocity : LREAL]]></Declaration>
      <Get Name="Get" Id="{cd238776-2665-438e-8843-6901647c425c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[SwitchVelocity := _SwitchVelocity;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{3a523177-e66f-47d0-8004-3fc4fbb2e246}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_SwitchVelocity := SwitchVelocity;]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>